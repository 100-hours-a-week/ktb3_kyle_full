plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.kyle'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = '0.8.12'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'com.h2database:h2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    // P6Spy
    implementation 'p6spy:p6spy:3.9.1'
    implementation 'com.github.gavlyukovskiy:datasource-decorator-spring-boot-autoconfigure:1.9.0'

    // google
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-gcp-starter', version: '1.2.5.RELEASE'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-gcp-storage', version: '1.2.5.RELEASE'

    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

}

tasks.named('test') {
    useJUnitPlatform()
}

def coverageExclusions = [
        'com/kyle/week4/config/**',
        'com/kyle/week4/security/**',
        'com/kyle/week4/aop/**',
        'com/kyle/week4/cache/InMemoryCache.class',
        'com/kyle/week4/utils/**',
        'com/kyle/week4/redis/**',
        'com/kyle/week4/exception/**',
        'com/kyle/week4/swagger/**',
        'com/kyle/week4/controller/docs',
        'com/kyle/week4/controller/request',
        'com/kyle/week4/controller/response',
        'com/kyle/week4/controller/AuthController.class',
        'com/kyle/week4/repository/**/*Memory*.class',
        'com/kyle/week4/P6spySqlFormatter.class',
        'com/kyle/week4/Week4Application.class',
]

tasks.jacocoTestReport {
    dependsOn(tasks.test)
    reports {
        xml.required.set(true)   // CI 커버리지 업로드 용
        html.required.set(true)  // build/reports/jacoco/test/html
        csv.required.set(false)
    }
    classDirectories.setFrom(
            files(classDirectories.files.collect { fileTree(dir: it, exclude: coverageExclusions) })
    )
}

tasks.test {
    useJUnitPlatform()
    finalizedBy(tasks.jacocoTestReport)
}